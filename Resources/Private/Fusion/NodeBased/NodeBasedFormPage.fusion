prototype(Neos.Form.Builder:NodeBasedFormPage) < prototype(Neos.Fusion:Renderer) {
    element {
        @context.page = ${this}
        identifier = ${pageNode.properties.identifier ? pageNode.properties.identifier : pageNode.identifier}
        label = ${pageNode.properties.label}
        renderingOptions._node = ${pageNode}
        renderingOptions._fusionPath = ${page.path}

        elements = Neos.Form.Builder:NodeBasedElementCollection {
            items = ${q(pageNode).children('elements').children()}
        }
    }
}

prototype(Neos.Form.Builder:NodeBasedPageCollection) < prototype(Neos.Fusion:Loop) {
    itemName = 'pageNode'
    itemRenderer = Neos.Fusion:Case {
        formElementTypeFromNodeType {
            condition = ${!Neos.Node.nodeType(pageNode).options.form.formElementType}
            renderer = Neos.Form.Builder:NodeBasedFormPage {
                type = ${pageNode.nodeTypeName.value + '.Definition'}
            }
        }

        default {
            condition = ${true}
            renderer = Neos.Form.Builder:NodeBasedFormPage {
                type = 'Neos.Form.Builder:FormPage.Definition'
                element.formElementType = ${Neos.Node.nodeType(pageNode).options.form.formElementType}
            }
        }
    }
}
